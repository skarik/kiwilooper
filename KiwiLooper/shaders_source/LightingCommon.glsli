vec3 calculate_world_position( float depth )
{
	// Start with view coords
	vec4 viewCoords = vec4(
		(gl_FragCoord.x / uViewInfo.x - 0.5) * 2.0,
		(gl_FragCoord.y / uViewInfo.y - 0.5) * -2.0,
		depth,
		1.0);
		
	// Transform to world coords
	vec4 worldCoords = uInverseViewProjection * viewCoords;
	worldCoords.xyz = worldCoords.xyz / worldCoords.w;
	
	return worldCoords.xyz;
}

float RectangleSolidAngle( vec3 worldPos, vec3 p0, vec3 p1, vec3 p2, vec3 p3)
{
	// Vector from each corner to the pixel being lit
	vec3 v0 = p0 - worldPos;
	vec3 v1 = p1 - worldPos;
	vec3 v2 = p2 - worldPos;
	vec3 v3 = p3 - worldPos;
	
	// Cross product of each edge
	vec3 n0 = normalize(cross(v0, v1));
	vec3 n1 = normalize(cross(v1, v2));
	vec3 n2 = normalize(cross(v2, v3));
	vec3 n3 = normalize(cross(v3, v0));
	
	// Angle between each edge
	float g0 = acos(dot(-n0, n1));
	float g1 = acos(dot(-n1, n2));
	float g2 = acos(dot(-n2, n3));
	float g3 = acos(dot(-n3, n0));

	// Sum the angles
	return g0 + g1 + g2 + g3 - 2.0 * 3.1415;
}

float LevelTotalLight( in float light, in float levels, in int style )
{
	float light_leveled = ceil(light * levels) / levels;

	float kLightLevelThreshold = 1.0 / levels;
	
	if (style == 0)
	{
		return light_leveled;
	}
	else if (style == kLightFalloff_Smooth_Brights)
	{
		return (light >  kLightLevelThreshold) ? light : light_leveled;
	}
	else if (style == kLightFalloff_Smooth_Darks)
	{
		return (light <= kLightLevelThreshold) ? light : light_leveled;
	}
	else if (style == kLightFalloff_Smooth_All)
	{
		return light;
	}
	
	return light_leveled;
}